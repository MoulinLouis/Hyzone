plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.gradle.plugin.idea-ext'
}

dependencies {
    implementation project(':hyvexa-core')
    compileOnly files("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar")
    runtimeOnly files("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar")
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = rootProject.file("run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

// Updates the manifest.json file with the latest properties defined in the
// build.properties file. Currently we update the version and if packs are
// included with the plugin.
tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

// Makes sure the plugin manifest is up to date.
tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

// Use a stable plugin jar name.
tasks.named('jar') {
    dependsOn ':hyvexa-core:jar'
    archiveFileName = "HyvexaParkour-${version}.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    from {
        configurations.runtimeClasspath.findAll { it.name != 'HytaleServer.jar' }
                .collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// Creates a run configuration in IDEA that will run the Hytale server with
// your plugin and the default assets.
if (project.extensions.findByName('idea') != null && idea.project != null) {
    idea.project.settings.runConfigurations {
        'HytaleServer'(org.jetbrains.gradle.ext.Application) {
            mainClass = 'com.hypixel.hytale.Main'
            moduleName = project.idea.module.name + '.main'
            programParameters = "--allow-op --assets=$hytaleHome/install/$patchline/package/game/latest/Assets.zip"
            if (includes_pack.toBoolean()) {
                programParameters += " --mods=${sourceSets.main.java.srcDirs.first().parentFile.absolutePath}"
            }
            if (load_user_mods.toBoolean()) {
                programParameters += " --mods=$hytaleHome/UserData/Mods"
            }
            workingDirectory = serverRunDir.absolutePath
        }
    }
}

tasks.register('HytaleServer', JavaExec) {
    group = 'application'
    description = 'Runs the Hytale server with the Hyvexa Parkour plugin.'
    classpath = files("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar")
    mainClass = 'com.hypixel.hytale.Main'
    args "--allow-op", "--assets=$hytaleHome/install/$patchline/package/game/latest/Assets.zip"
    if (includes_pack.toBoolean()) {
        args "--mods=${sourceSets.main.java.srcDirs.first().parentFile.absolutePath}"
    }
    if (load_user_mods.toBoolean()) {
        args "--mods=$hytaleHome/UserData/Mods"
    }
    workingDir = serverRunDir
}
